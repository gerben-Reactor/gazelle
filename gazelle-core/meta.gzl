grammar Meta {
    terminals {
        IDENT: Ident,
        KW_GRAMMAR,
        KW_TERMINALS,
        KW_PREC,
        LBRACE,
        RBRACE,
        COLON,
        COMMA,
        EQ,
        PIPE,
        SEMI,
        AT
    }

    grammar_def: GrammarDef = KW_GRAMMAR IDENT LBRACE sections RBRACE @grammar_def;

    sections: Sections = sections section @sections_append
                       | section @sections_single;

    section: Section = terminals_block @section_terminals
                     | rule @section_rule;

    terminals_block: TerminalsBlock = KW_TERMINALS LBRACE terminal_list COMMA RBRACE @terminals_trailing
                                    | KW_TERMINALS LBRACE terminal_list RBRACE @terminals_block
                                    | KW_TERMINALS LBRACE RBRACE @terminals_empty;

    terminal_list: TerminalList = terminal_list COMMA terminal_item @terminal_list_append
                                | terminal_item @terminal_list_single;

    terminal_item: TerminalItem = KW_PREC IDENT COLON IDENT @terminal_prec_typed
                                | KW_PREC IDENT @terminal_prec_untyped
                                | IDENT COLON IDENT @terminal_typed
                                | IDENT @terminal_untyped;

    rule: Rule = IDENT COLON IDENT EQ alts SEMI @rule_typed
               | IDENT EQ alts SEMI @rule_untyped;

    alts: Alts = alts PIPE alt @alts_append
               | alts PIPE @alts_empty
               | alt @alts_single;

    alt: Alt = seq AT IDENT @alt_named
             | seq @alt_unnamed
             | AT IDENT @alt_empty_named;

    seq: Seq = seq IDENT @seq_append
             | IDENT @seq_single;
}
