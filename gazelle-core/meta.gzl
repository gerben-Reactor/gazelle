grammar Meta {
    terminals {
        IDENT: Ident,
        KW_GRAMMAR,
        KW_TERMINALS,
        KW_PREC,
        LBRACE,
        RBRACE,
        COLON,
        COMMA,
        EQ,
        PIPE,
        SEMI,
        AT
    }

    grammar_def: GrammarDef = KW_GRAMMAR IDENT LBRACE sections RBRACE @grammar_def;

    sections: Sections = sections section @sections_append
                       | section @sections_single;

    section: Section = terminals_block @section_terminals
                     | rule @section_rule;

    terminals_block: TerminalsBlock = KW_TERMINALS LBRACE terminal_list COMMA RBRACE @terminals_trailing
                                    | KW_TERMINALS LBRACE terminal_list RBRACE @terminals_block
                                    | KW_TERMINALS LBRACE RBRACE @terminals_empty;

    terminal_list: TerminalList = terminal_list COMMA terminal_item @terminal_list_append
                                | terminal_item @terminal_list_single;

    terminal_item: TerminalItem = prec_opt IDENT type_opt @terminal_item;

    prec_opt: PrecOpt = KW_PREC @prec_yes | @prec_no;

    type_opt: TypeOpt = COLON IDENT @type_some | @type_none;

    rule: Rule = IDENT COLON IDENT EQ alts SEMI @rule_typed
               | IDENT EQ alts SEMI @rule_untyped;

    alts: Alts = alts PIPE alt @alts_append
               | alt @alts_single;

    alt: Alt = seq name_opt @alt
             | name_opt @alt_empty;

    name_opt: NameOpt = AT IDENT @name_some | @name_none;

    seq: Seq = seq IDENT @seq_append
             | IDENT @seq_single;
}
