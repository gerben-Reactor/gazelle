start grammar_def;
terminals {
    IDENT: Ident,
    NUM: Ident,
    KW_START,
    KW_TERMINALS,
    KW_PREC,
    KW_EXPECT,
    KW_MODE,
    UNDERSCORE,
    LBRACE,
    RBRACE,
    LPAREN,
    RPAREN,
    COLON,
    COMMA,
    EQ,
    PIPE,
    SEMI,
    AT,
    QUESTION,
    STAR,
    PLUS,
    PERCENT
}

grammar_def =
    KW_START IDENT SEMI
    mode_decl?
    expect_decl*
    KW_TERMINALS LBRACE (terminal_item % COMMA) RBRACE
    rule+
    @grammar_def;

mode_decl = KW_MODE IDENT SEMI @mode_decl;

expect_decl = KW_EXPECT NUM IDENT SEMI @expect_decl;

terminal_item = KW_PREC? IDENT type_annot? @terminal_item;

type_annot = COLON IDENT @type_annot;

rule = IDENT EQ (alt % PIPE) SEMI @rule;

alt = term+ action_name? @alt;

action_name = AT IDENT @action_name;

term = LPAREN IDENT PERCENT IDENT RPAREN @sym_sep
     | IDENT QUESTION @sym_opt
     | IDENT STAR @sym_star
     | IDENT PLUS @sym_plus
     | IDENT @sym_plain
     | UNDERSCORE @sym_empty;
