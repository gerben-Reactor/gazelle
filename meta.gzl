grammar Meta {
    start grammar_def;
    terminals {
        IDENT: Ident,
        KW_GRAMMAR,
        KW_START,
        KW_TERMINALS,
        KW_PREC,
        LBRACE,
        RBRACE,
        COLON,
        COMMA,
        EQ,
        PIPE,
        SEMI,
        AT,
        QUESTION,
        STAR,
        PLUS
    }

    grammar_def: GrammarDef = KW_GRAMMAR IDENT LBRACE KW_START IDENT SEMI terminals_block rule+ RBRACE @grammar_def;

    terminals_block: TerminalsBlock = KW_TERMINALS LBRACE terminal_list COMMA RBRACE @terminals_trailing
                                    | KW_TERMINALS LBRACE terminal_list RBRACE @terminals_block
                                    | KW_TERMINALS LBRACE RBRACE @terminals_empty;

    terminal_list: TerminalList = terminal_list COMMA terminal_item @terminal_list_append
                                | terminal_item @terminal_list_single;

    terminal_item: TerminalItem = KW_PREC? IDENT type_annot? @terminal_item;

    type_annot: Ident = COLON IDENT @type_annot;

    rule: Rule = IDENT type_annot? EQ alts SEMI @rule;

    alts: Alts = alts PIPE alt @alts_append
               | alt @alts_single;

    alt: Alt = symbol+ action_name? @alt
             | action_name? @alt_empty;

    action_name: Ident = AT IDENT @action_name;

    symbol: Symbol = IDENT QUESTION @sym_opt
                   | IDENT STAR @sym_star
                   | IDENT PLUS @sym_plus
                   | IDENT @sym_plain;
}
