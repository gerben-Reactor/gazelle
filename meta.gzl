grammar Meta {
    start grammar_def;
    terminals {
        IDENT: Ident,
        KW_GRAMMAR,
        KW_START,
        KW_TERMINALS,
        KW_PREC,
        LBRACE,
        RBRACE,
        COLON,
        COMMA,
        EQ,
        PIPE,
        SEMI,
        AT,
        QUESTION,
        STAR,
        PLUS
    }

    grammar_def: GrammarDef = 
        KW_GRAMMAR IDENT LBRACE 
            KW_START IDENT SEMI 
            terminals_block 
            rule+ 
        RBRACE @grammar_def;

    terminals_block: TerminalsBlock = KW_TERMINALS LBRACE terminal_item* RBRACE @terminals_block;

    terminal_item: TerminalItem = KW_PREC? IDENT type_annot? COMMA? @terminal_item;

    type_annot: Ident = COLON IDENT @type_annot;

    rule: Rule = IDENT type_annot? EQ alts SEMI @rule;

    alts: Alts = alt_list empty_alt? @alts;

    alt_list: AltList = alt_list PIPE alt @alt_list_append
                      | alt @alt_list_single;

    alt: Alt = symbol+ action_name? @alt;

    empty_alt: Alt = PIPE action_name? @alt_empty;

    action_name: Ident = AT IDENT @action_name;

    symbol: Symbol = IDENT QUESTION @sym_opt
                   | IDENT STAR @sym_star
                   | IDENT PLUS @sym_plus
                   | IDENT @sym_plain;
}
